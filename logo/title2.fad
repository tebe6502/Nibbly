color_nr = 6
;-----------;
;-  WAIT   -;
;-----------;
.proc wait
 lda:cmp:req cloc
 dex
 bne wait
 rts
.endp

;-----------;
;-  FADE   -;
;-----------;
.proc fade
 stx adr+1
 sty adr+2

 ldx #6
 jsr wait
 
 mva #16 licz
fad
 ldx #2
 jsr wait

adr jsr $ffff

 dec licz
 bne fad
 rts

licz brk
.endp

;-----------;
;- FADE IN -;
;-----------;
.proc fade_in

 ldx <fade
 ldy >fade
 jmp :fade

fade

 mva <tab.l_tcol  adr0+1
 mva <tab.h_tcol  adr1+1
 mva <tab.t_satur adr2+1
 mva <tab.t_color adr3+1

 mva >tab.l_tcol  adr0+2
 mva >tab.h_tcol  adr1+2
 mva >tab.t_satur adr2+2
 mva >tab.t_color adr3+2

 mvx #0 count

fade_l
adr0 lda $ffff,x
 sta ftmp
adr1 lda $ffff,x
 sta ftmp+1

 ldy #1
 lda (ftmp),y
 tay

 and #$f0
adr3 cmp $ffff,x
 beq skp1

 tya
 add #$10
 tay

skp1

 tya
 and #$0f
adr2 cmp $ffff,x
 beq skp0

 iny

skp0

 tya
 ldy #1
 sta (ftmp),y

 inx
 bne skp
 inc count

 inc adr0+2
 inc adr1+2
 inc adr2+2
 inc adr3+2

skp
 cpx <color_nr
 bne fade_l
 lda count
 cmp >color_nr
 bne fade_l
 rts

count brk
.endp

;------------;
;- FADE OUT -;
;------------;
.proc fade_out

 ldx <fade
 ldy >fade
 jmp :fade

fade

 mva <tab.l_tcol adr0+1
 mva <tab.h_tcol adr1+1

 mva >tab.l_tcol adr0+2
 mva >tab.h_tcol adr1+2

 mvx #0 count

fade_l
adr0 lda $ffff,x
 sta ftmp
adr1 lda $ffff,x
 sta ftmp+1

 ldy #1
 lda (ftmp),y
 tay

 and #$0f
 beq skp0

 dey

skp0

 tya
 and #$f0
 beq skp1

 tya
 sub #$10
 tay

skp1
 tya

 ldy #1
 sta (ftmp),y

 inx
 bne skp
 inc count

 inc adr0+2
 inc adr1+2

skp
 cpx <color_nr
 bne fade_l
 lda count
 cmp >color_nr
 bne fade_l
 rts

count brk
.endp

;--------------;
;- SAVE COLOR -;
;--------------;
.proc save_color

 mva <tab.l_tcol  adr0+1
 mva <tab.h_tcol  adr1+1
 mva <tab.t_satur adr2+1
 mva <tab.t_color adr3+1

 mva >tab.l_tcol  adr0+2
 mva >tab.h_tcol  adr1+2
 mva >tab.t_satur adr2+2
 mva >tab.t_color adr3+2

 mvx #0 count

save_l
adr0 lda $ffff,x
 sta ftmp
adr1 lda $ffff,x
 sta ftmp+1

 ldy #1
 lda (ftmp),y
 pha
 and #$0f
adr2 sta $ffff,x
 pla
 and #$f0
adr3 sta $ffff,x

 lda #0        ; black screen (all colors = $00)
 sta (ftmp),y

 inx
 bne skp
 inc count

 inc adr0+2
 inc adr1+2
 inc adr2+2
 inc adr3+2

skp
 cpx <color_nr
 bne save_l
 lda count
 cmp >color_nr
 bne save_l
 rts

count brk
.endp

.proc tab
l_tcol
 dta l(c0,c1,c2,c3,c4,c5)

h_tcol
 dta h(c0,c1,c2,c3,c4,c5)

t_satur .ds color_nr
t_color .ds color_nr
.endp
